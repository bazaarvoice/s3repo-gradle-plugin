group 'com.bazaarvoice.gradle'
version '1.0.8'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk-s3:1.11.490'
    compile 'commons-io:commons-io:2.6'
    compile gradleApi()
}

pluginBundle {
    website = 'https://github.com/bazaarvoice/s3repo-gradle-plugin'
    vcsUrl = 'https://github.com/bazaarvoice/s3repo-gradle-plugin'
    description = 'Plugin to publish to an S3 Yum repository'
    tags = ['s3', 'repo', 'yum', 'repository', 'publish']

    plugins {
        s3RepoPlugin {
            id = 'com.bazaarvoice.s3repo'
            displayName = 'S3 Yum Repo Publish Plugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().appendNode('parent').with {
                    appendNode('groupId', 'com.bazaarvoice.commons')
                    appendNode('artifactId', 'bv-super-pom')
                    appendNode('version', '1.21')
                    appendNode('relativePath')
                }
            }

            from components.java
        }
    }
}

publishing {
    repositories {
        mavenCentral()
    }
}

/* Because the maven-publish plugin doesn't read settings.xml, we run 'mvn deploy' manually */
task deploy(type:Exec, dependsOn: 'generatePomFileForMavenJavaPublication') {
    commandLine 'mvn', 'deploy', '-f', 'build/publications/mavenJava/pom-default.xml'
}