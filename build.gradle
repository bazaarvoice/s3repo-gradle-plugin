group 'com.bazaarvoice.gradle'
version '1.0.2'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.gradle.plugin-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk:1.9.1'
    compile 'commons-io:commons-io:1.4'
    compile gradleApi()
}

pluginBundle {
    website = 'https://github.com/louiswilliams/s3repo-gradle-plugin'
    vcsUrl = 'https://github.com/louiswilliams/s3repo-gradle-plugin'
    description = 'Plugin to deploy to an S3 Yum repository'
    tags = ['s3', 'repo', 'yum', 'repository', 'deploy']

    plugins {
        s3RepoPlugin {
            id = 'com.bazaarvoice.s3repo'
            displayName = 'S3 Yum Repo Deploy Plugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().appendNode('parent').with {
                    appendNode('groupId', 'com.bazaarvoice.commons')
                    appendNode('artifactId', 'bv-super-pom')
                    appendNode('version', '1.11')
                    appendNode('relativePath')
                }
            }

            from components.java
        }
    }
}

publishing {
    repositories {
        mavenCentral()
    }
}

/* Because the maven-publish plugin doesn't read settings.xml, we run 'mvn deploy' manually */
task deploy(type:Exec, dependsOn: 'generatePomFileForMavenJavaPublication') {
    commandLine 'mvn', 'deploy', '-f', 'build/publications/mavenJava/pom-default.xml'
}